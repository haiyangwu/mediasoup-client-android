apply plugin: 'com.android.library'

// Load local properties.
Properties properties = new Properties()
properties.load(project.rootProject.file('local.properties').newDataInputStream())

// Read webrtc dir configurations.
def WEBRTC_INCLUDE_PATH = properties.getProperty('webrtc.include.path')
def WEBRTC_BINARY_PATH = properties.getProperty('webrtc.binary.path')
def WEBRTC_JAR_PATH = properties.getProperty('webrtc.jar.path')

// Check webrtc dir configurations.
if (!WEBRTC_INCLUDE_PATH) {
    throw new GradleException('please define webrtc.include.path in local.properties')
}
if (!WEBRTC_BINARY_PATH) {
    throw new GradleException('please define webrtc.binary.path in local.properties')
}
if (!WEBRTC_JAR_PATH) {
    throw new GradleException('please define webrtc.jar.path in local.properties')
}

android {
    compileSdkVersion 28

    defaultConfig {
        minSdkVersion 18
        targetSdkVersion 28
        versionCode 1
        versionName "3.0.4"

        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"

        externalNativeBuild {
            cmake {
                // Passes optional arguments to CMake.
                arguments "-DLIBWEBRTC_INCLUDE_PATH=" + WEBRTC_INCLUDE_PATH,
                        "-DLIBWEBRTC_BINARY_PATH=" + WEBRTC_BINARY_PATH,
                        "-DMEDIASOUPCLIENT_BUILD_TESTS=OFF",
                        "-DSDPTRANSFORM_BUILD_TESTS=OFF",
                        "-DSDPTRANSFORM_BUILD_READMEHELPER=OFF"
            }

            ndk {
                // Specifies the ABI configurations of your native
                // libraries Gradle should build and package with your APK.
                abiFilters 'x86' /*,'x86_64', 'armeabi-v7a', 'arm64-v8a'*/
            }
        }
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    externalNativeBuild {
        cmake {
            // Provides a relative path to your CMake build script.
            path "CMakeLists.txt"
        }
    }
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    implementation fileTree(dir: WEBRTC_JAR_PATH, include: ['libwebrtc.jar'])

    implementation 'com.android.support:appcompat-v7:28.0.0'
    testImplementation 'junit:junit:4.12'
    androidTestImplementation 'com.android.support.test:runner:1.0.2'
    androidTestImplementation 'com.android.support.test.espresso:espresso-core:3.0.2'
}
